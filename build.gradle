buildscript {
    repositories {
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath 'com.google.protobuf:protobuf-gradle-plugin:0.8.14'
    }
}

plugins {
    id 'java'
    id "com.google.protobuf" version "0.8.14"
}

group 'com.sunya.electionguard'
version '0.5-SNAPSHOT'

repositories {
    // Prefer JCenter to Maven Central. See
    // https://blog.bintray.com/2015/02/09/android-studio-migration-from-maven-central-to-jcenter/
    jcenter()
    mavenCentral()  // JCenter isn't quite a superset of Maven Central.
}

ext.junitPlatformVersion = '1.7.0'
ext.junitJupiterVersion = '5.7.0'
ext.jqwikVersion = '1.3.10'

sourceSets {
    integration {
        java.srcDir "$projectDir/src/integration/java"
        resources.srcDir "$projectDir/src/integration/resources"
        compileClasspath += main.output + test.output
        runtimeClasspath += main.output + test.output
    }
}

sourceSets {
    testproperties {
        java.srcDir "$projectDir/src/testproperties/java"
        resources.srcDir "$projectDir/src/testproperties/resources"
        compileClasspath += main.output + test.output
        runtimeClasspath += main.output + test.output
    }
}

configurations {
    integrationImplementation.extendsFrom testImplementation
    integrationRuntime.extendsFrom testRuntime
}

configurations {
    testpropertiesImplementation.extendsFrom testImplementation
    testpropertiesRuntime.extendsFrom testRuntime
}

compileTestJava {
    // To enable argument names in reporting and debugging
    options.compilerArgs += '-parameters'
}

test {
    useJUnitPlatform {
        includeEngines 'jqwik'

        // Or include several Junit engines if you use them
        // includeEngines 'jqwik', 'junit-jupiter', 'junit-vintage'

        // includeTags 'fast', 'medium'
        // excludeTags 'slow'
    }

    include '**/*Properties.class'
    include '**/*Test.class'
    include '**/*Tests.class'
    include '**/Test*.class'
}

dependencies {
    // Annotations: Nullable
    implementation 'com.google.code.findbugs:jsr305:3.0.2'
    implementation 'com.google.guava:guava:28.0-jre'
    implementation 'com.google.protobuf:protobuf-java:3.14.0'
    implementation 'com.google.code.gson:gson:2.8.6'
    implementation "com.google.auto.value:auto-value-annotations:1.7.4"
    implementation 'com.google.flogger:flogger:0.5.1'
    runtimeOnly 'com.google.flogger:flogger-system-backend:0.5.1'

    annotationProcessor "com.google.auto.value:auto-value:1.7.4"

    testImplementation 'com.google.truth:truth:1.1'
    testImplementation 'com.google.truth.extensions:truth-java8-extension:1.1'

    // aggregate jqwik dependency
    testImplementation "net.jqwik:jqwik:${jqwikVersion}"

    // Add if you also want to use the Jupiter engine or Assertions from it
    testImplementation "org.junit.jupiter:junit-jupiter:5.7.0"
}

protobuf {
    protoc {
        // The artifact spec for the Protobuf Compiler
        artifact = 'com.google.protobuf:protoc:3.14.0'
    }
}

// Add generated sources to the main sourceSet, which keeps intellij happy.
sourceSets.main.java.srcDirs += ['build/generated/source/proto/main/java']